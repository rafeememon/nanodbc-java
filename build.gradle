// https://community.sonarsource.com/t/error-locking-filebasedconfig-root-config-jgit-config-failed-after-5-retries
buildscript {
  configurations.classpath {
    resolutionStrategy {
      force 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'
    }
  }
}

plugins {
  id 'com.palantir.git-version' version "${gitVersionVersion}"
  // id 'com.diffplug.spotless' version "${spotlessVersion}"
  id 'java-library'
  id 'eclipse'
  id 'maven-publish'
}

apply from: "${rootDir}/gradle/s3.gradle"
// apply from: "${rootDir}/gradle/spotless.gradle"

group = 'io.nanodbc'
version = gitVersion()

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
  mavenCentral()
}

dependencyLocking {
  lockAllConfigurations()
}

configurations {
  javacpp
}

dependencies {
  javacpp group: 'org.bytedeco', name: 'javacpp', version: javacppVersion
  implementation group: 'org.bytedeco', name: 'javacpp', version: javacppVersion
}

java {
  withSourcesJar()
}

jar {
  // Don't include unnecessary compiler outputs.
  exclude '**/*.exp', '**/*.lib', '**/*.obj'
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
    }
  }
}

def nanodbcSrcPath = "${rootDir}\\nanodbc"
def nanodbcExtSrcPath = "${rootDir}\\nanodbc_ext"
def nanodbcCmakePath = "${buildDir}\\cmake"
def nanodbcMsbuildPath = "${buildDir}\\msbuild"

task cmakeNanodbc(type: Exec) {
  inputs.file "${rootDir}\\CMakeLists.txt"
  inputs.file "${nanodbcSrcPath}\\CMakeLists.txt"
  inputs.file "${nanodbcExtSrcPath}\\CMakeLists.txt"
  outputs.dir nanodbcCmakePath

  workingDir nanodbcCmakePath
  commandLine 'cmake', "${rootDir}"
}

task msbuildNanodbc(type: Exec) {
  dependsOn cmakeNanodbc

  inputs.dir nanodbcSrcPath
  inputs.dir nanodbcExtSrcPath
  inputs.dir nanodbcCmakePath
  outputs.dir nanodbcMsbuildPath

  workingDir nanodbcCmakePath
  commandLine 'msbuild', 'ALL_BUILD.vcxproj', "/p:OutDir=${nanodbcMsbuildPath};Configuration=Release"
}

task compileJavacpp(type: JavaExec) {
  dependsOn compileJava, msbuildNanodbc

  // TODO: Set inputs and outputs

  workingDir = tasks.compileJava.destinationDir
  classpath = configurations.javacpp
  mainClass = 'org.bytedeco.javacpp.tools.Builder'
  args = [
    'io.nanodbc.impl.*',
    '-o', 'jninanodbc',
    '-Xcompiler', "${nanodbcMsbuildPath}\\nanodbc.lib",
    '-Xcompiler', "${nanodbcMsbuildPath}\\nanodbc_ext.lib",
    '-Xcompiler', 'odbc32.lib',
    '-Xcompiler', 'odbccp32.lib',
    '-Xcompiler', "/I${nanodbcSrcPath}",
    '-Xcompiler', "/I${nanodbcExtSrcPath}"
  ]
}

classes.dependsOn compileJavacpp
