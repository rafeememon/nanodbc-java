plugins {
  id 'java-library'
  id 'eclipse'
  id 'maven-publish'
  id 'com.palantir.git-version' version '0.10.1'
}

apply from: 'gradle/s3.gradle'

group = 'io.nanodbc'
version = gitVersion()

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
  mavenCentral()
}

configurations {
  javacpp
}

dependencies {
  javacpp group: 'org.bytedeco', name: 'javacpp', version: javacppVersion
  implementation group: 'org.bytedeco', name: 'javacpp', version: javacppVersion
}

jar {
  // Don't include unnecessary compiler outputs.
  exclude '**/*.exp', '**/*.lib', '**/*.obj'
}

task sourceJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allJava
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar
    }
  }
}

def nanodbcPath = "${rootDir.toString()}\\nanodbc"
def nanodbcExtPath = "${rootDir.toString()}\\nanodbc_ext"
def nanodbcJavaOutputs = sourceSets.main.output.getFiles()

task cmakeNanodbc(type: Exec) {
  commandLine 'cmake', '.'
}

task buildNanodbc(type: Exec) {
  dependsOn cmakeNanodbc

  commandLine 'msbuild'
  args = [
    'ALL_BUILD.vcxproj',
    '/p:Configuration=Release'
  ]
}

task processJavacpp(type: JavaExec) {
  dependsOn compileJava, processResources, buildNanodbc

  classpath = configurations.javacpp
  main = 'org.bytedeco.javacpp.tools.Builder'
  args = [
    '-cp', nanodbcJavaOutputs.join(File.pathSeparator),
    '-o', 'jninanodbc',
    '-Xcompiler', "${nanodbcPath}\\Release\\nanodbc.lib",
    '-Xcompiler', "${nanodbcExtPath}\\Release\\nanodbc_ext.lib",
    '-Xcompiler', 'odbc32.lib',
    '-Xcompiler', 'odbccp32.lib',
    '-Xcompiler', "/I${nanodbcPath}",
    '-Xcompiler', "/I${nanodbcExtPath}"
  ]
}

classes.dependsOn processJavacpp
